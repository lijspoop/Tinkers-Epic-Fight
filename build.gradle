buildscript {
	repositories {
		mavenLocal()
		maven { url = 'https://maven.minecraftforge.net/' }
		maven { url = 'https://libraries.minecraft.net/' }
		jcenter()
		mavenCentral()
		maven {
			name = "sonatype"
			url = "https://oss.sonatype.org/content/repositories/snapshots/"
		}
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
	}
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

group = modGroup // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = modId
version = "${minecraftVersion}-${specificationVersion}"

java.toolchain.languageVersion = JavaLanguageVersion.of(modJavaVersion)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
	mappings channel: 'official', version: minecraftVersion
	runs {
		client {
			workingDirectory project.file('run')
			property 'forge.logging.markers', 'REGISTRIES'

			// Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
			property 'forge.logging.console.level', 'debug'
			args '--mod', 'tinkersepicfight', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/'), '--existing-mod', 'tconstruct'

			mods {
				tinkersepicfight {
					source sourceSets.main
				}
			}
		}

		server {
			workingDirectory project.file('run')

			property 'forge.logging.markers', 'REGISTRIES'

			property 'forge.logging.console.level', 'debug'
			args '--mod', 'tinkersepicfight', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/'), '--existing-mod', 'tconstruct', '--existing-mod', 'epicfight'

			mods {
				tinkersepicfight {
					source sourceSets.main
				}
			}
		}

		data {
			workingDirectory project.file('run')

			property 'forge.logging.markers', 'REGISTRIES'

			property 'forge.logging.console.level', 'debug'

			// Specify the modid for data generation, where: output the resulting resource, and where: look for existing resources.
			args '--mod', 'tinkersepicfight', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/'), '--existing-mod', 'tconstruct'

			mods {
				tinkersepicfight {
					source sourceSets.main
				}
			}
		}
	}
}

// Include resources generated by data generators.
sourceSets {
	main {
		resources {
			srcDirs "src/generated/resources"
			//But exclude the cache of the generated data from what gets built
			exclude '.cache'
		}
	}
}

repositories {
	mavenCentral()
	mavenLocal()
	maven { // Vanilla
		name 'Vanilla'
		url 'https://libraries.minecraft.net/'
	}
	maven { // Forge
		name 'ForgeFS'
		url 'https://maven.minecraftforge.net/'
	}
	maven { // Mantle and JEI
		name 'DVS1 Maven FS'
		url 'https://dvs1.progwml6.com/files/maven'
	}
	maven { //:P
		name 'tterrag maven'
		url "https://maven.tterrag.com/"
	}
	maven { // CraftTweaker
		name 'BlameJared Maven'
		url 'https://maven.blamejared.com'
	}
	flatDir {
		dir 'libs'
	}
//    maven {
//        url "https://www.cursemaven.com"
//        content {
//            includeGroup "curse.maven"
//        }
//    }
}

dependencies {
	minecraft 'net.minecraftforge:forge:1.16.5-36.2.34'

	compileOnly fg.deobf("mezz.jei:jei-1.16.5:7.8.0.1009:api")
	runtimeOnly fg.deobf("mezz.jei:jei-1.16.5:7.8.0.1009")

	implementation fg.deobf("slimeknights.mantle:Mantle:1.16.5-1.6.157")
	implementation fg.deobf("slimeknights.tconstruct:TConstruct:1.16.5-3.3.4.335")
	implementation fg.deobf("com.yesman.epicfight:epicfight:16.6.4")
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
}

// Example for how: get properties into the manifest for reading by the runtime..
tasks.jar {
	manifest {
		attributes([
						"Specification-Title"     : project.name,
						"Specification-Vendor"    : modAuthor,
						"Specification-Version"   : specificationVersion,
						"Implementation-Title"    : modId,
						"Implementation-Version"  : archiveVersion,
						"Implementation-Vendor"   : modAuthor,
						"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
						'Built-For'               : "${minecraftVersion}-${forgeVersion}",
						'Built-At-UTC'            : new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
						'Built-On-JVM'            : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
		])
	}
	finalizedBy('reobfJar')
}

tasks.register('sourcesJar', Jar) {
	archiveClassifier.set("sources")
	from sourceSets.main.allJava
}

tasks.register('deobfJar', Jar) {
	archiveClassifier.set("deobf")
	from sourceSets.main.output
}

artifacts {
	archives sourcesJar
	archives deobfJar
}

tasks.withType(ProcessResources).configureEach {
	// This will ensure that this task is redone when the versions change.
	inputs.property('version', specificationVersion)

	filesMatching("META-INF/mods.toml") {
		expand([
						"forgeVersionRange"     : forgeVersionRange,
						"githubUrl"             : githubUrl,
						"githubRaw"             : githubRaw,
						"loaderVersionRange"    : loaderVersionRange,
						"minecraftVersion"      : minecraftVersion,
						"minecraftVersionRange" : minecraftVersionRange,
						"epicfightVersion"      : epicfightVersion,
						"epicfightVersionRange" : epicfightVersionRange,
						"tconstructVersion"     : tconstructVersion,
						"tconstructVersionRange": tconstructVersionRange,
						"modAuthor"             : modAuthor,
						"modDescription"        : modDescription,
						"modId"                 : modId,
						"modJavaVersion"        : modJavaVersion,
						"modName"               : modName,
						"file"                  : [jarVersion: specificationVersion]
		])
	}
}